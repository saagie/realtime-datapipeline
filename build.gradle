plugins {
    id 'scala'
    id 'java'
    id 'com.adarshr.test-logger' version '1.1.2'
    id 'com.github.maiflai.scalatest' version '0.23'
    id 'com.github.johnrengelman.shadow' version '4.0.1'
}

group = "io.saagie.academy"
version = "1.0.0-SNAPSHOT"

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
    maven {
        url "https://dl.bintray.com/cakesolutions/maven/"
    }
}

configurations {
    compile.exclude module: 'slf4j-log4j12'
    all {
        resolutionStrategy {
            force 'com.fasterxml.jackson.core:jackson-databind:2.6.7.1'
        }
    }
}

dependencies {
    compile group: 'org.scala-lang', name: 'scala-library', version: "$scalaMajor.$scalaMinor"
    compile group: 'org.scala-lang.modules', name: "scala-xml_$scalaMajor", version: "1.1.1"
    compile group: 'org.scalactic', name: "scalactic_$scalaMajor", version: scalaTest

    //logs
    compile group: 'org.codehaus.groovy', name: 'groovy', version: groovy
    compile group: 'ch.qos.logback', name: 'logback-classic', version: logback

    //Kafka
    compile group: 'net.cakesolutions', name: "scala-kafka-client_$scalaMajor", version: kafkaClients


    compile group: 'org.apache.kafka', name: 'kafka-clients', version: kafkaClients
    compile group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaClients
    compile group: 'org.apache.kafka', name: "kafka-streams-scala_$scalaMajor", version: kafkaClients

    //Spark
    compileOnly group: 'org.apache.spark', name: "spark-core_$scalaMajor", version: spark
    compileOnly group: 'org.apache.spark', name: "spark-streaming_$scalaMajor", version: spark
    compileOnly group: 'org.apache.spark', name: "spark-sql_$scalaMajor", version: spark
    compile group: 'org.apache.spark', name: "spark-streaming-kafka-0-10_$scalaMajor", version: spark
    compile group: 'org.apache.spark', name: "spark-sql-kafka-0-10_$scalaMajor", version: spark

    //Generator
    compile group: 'com.github.azakordonets', name: "fabricator_$scalaMajor", version: fabricator
    compile group: 'org.json4s', name: "json4s-native_$scalaMajor", version: scala4s

    //Test
    testCompile group: 'net.manub', name: "scalatest-embedded-kafka_$scalaMajor", version: kafkaClients
    testCompile group: 'org.scalatest', name: "scalatest_$scalaMajor", version: scalaTest
    testCompile group: 'org.pegdown', name: 'pegdown', version: pegDown
    testCompile group: 'com.holdenkarau', name: "spark-testing-base_$scalaMajor", version: "${spark}_0.10.0"
}

test {
    maxParallelForks = 1
}

ext.mainClassName = 'io.saagie.academy.datapipeline.kafka.streams.StreamConsumer'

shadowJar {
    zip64 = true
    classifier = ""
    manifest {
        attributes "Main-Class": mainClassName
    }
}
